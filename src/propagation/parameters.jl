struct Parameters{T <: AbstractFloat}
    # Propagation parameters
    maxsteps::Int
    μ_ast::Vector{T}
    order::Int
    abstol::T
    parse_eqs::Bool
    # Residuals parameters
    mpc_catalogue_codes_201X::Vector{String}
    truth::String
    resol::Resolution
    bias_matrix::Matrix{T}
    # Least squares fit parameters
    niter::Int
    # Gauss Method parameters
    max_triplets::Int
    varorder::Int
    Q_max::T
    # Admissible Region parameters
    maxiter::Int
    # Outlier rejection parameters
    max_per::T
    # Inner constructor is generated by default
end

# Outer constructors

@doc raw"""
    Parameters([params::Parameters{T};] kwargs...) where {T <: AbstractFloat}

Parameters for all orbit determination functions.

# Propagation Parameters

- `maxsteps::Int`: maximum number of steps for the integration (default: `500`).
- `μ_ast::Vector{T}`: vector of gravitational parameters (default: `μ_ast343_DE430[1:end]`).
- `order::Int`: order of Taylor expansions wrt time (default: 25).
- `abstol::T`: absolute tolerance used to compute propagation timestep (default: `1e-20`).
- `parse_eqs::Bool`: whether to use the specialized method of `jetcoeffs` or not (default: `true`).

# Residuals Parameters

- `debias_table::String`: debiasing scheme (default: `"2018"`). Possible values are:
    - `"2014"` corresponds to https://doi.org/10.1016/j.icarus.2014.07.033,
    - `"2018"` corresponds to https://doi.org/10.1016/j.icarus.2019.113596,
    - `"hires2018"` corresponds to https://doi.org/10.1016/j.icarus.2019.113596.

# Least Squares Fit Parameters

- `niter::Int`: number of iterations for differential corrections / Newton's method (default: `5`).

# Gauss Method Parameters

- `max_triplets::Int`: maximum number of triplets to check for a solution (default: `10`).
- `varorder::Int`: order of jet transport perturbation (default: `5`). 
- `Q_max::T`: maximum nrms that is considered a good enough orbit (default: `5.0`).

# Admissible Region Parameters

- `maxiter::Int`: maximum number of iterations for admissible region `ADAM` optimizer (default: `100`).

# Outlier Rejection Parameters

- `max_per::T`: maximum allowed rejection percentage (default: `18.0`).
"""
function Parameters(; maxsteps::Int = 500, μ_ast::Vector{T} = μ_ast343_DE430[1:end],
                      order::Int = 25, abstol::T = 1e-20, parse_eqs::Bool = true, 
                      debias_table::String = "2018", niter::Int = 5, max_triplets::Int = 10,
                      varorder::Int = 5, Q_max::T = 5.0, maxiter::Int = 100,
                      max_per::T = 18.0) where {T <: AbstractFloat}
    # Unfold debiasing matrix
    mpc_catalogue_codes_201X, truth, resol, bias_matrix = select_debiasing_table(debias_table)
    # Assemble Parameters
    return Parameters{T}(maxsteps, μ_ast, order, abstol, parse_eqs, mpc_catalogue_codes_201X,
                         truth, resol, bias_matrix, niter, max_triplets, varorder, Q_max,
                         maxiter, max_per)
end                            

function Parameters(params::Parameters{T}; kwargs...) where {T <: AbstractFloat}
    fields = fieldnames(Parameters{T})
    vals = Vector{Any}(undef, length(fields))
    for i in eachindex(vals)
        if fields[i] in keys(kwargs)
            vals[i] = kwargs[i]
        else
            vals[i] = getfield(params, i)
        end
    end

    return Parameters{T}(vals...)
end

function residuals(obs::Vector{RadecMPC{T}}, params::Parameters{T}; kwargs...) where {T <: AbstractFloat}
    return residuals(obs, params.mpc_catalogue_codes_201X, params.truth, params.resol,
                     params.bias_matrix; kwargs...)
end